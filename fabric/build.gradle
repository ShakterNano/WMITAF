plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "me.shedaniel.unified-publishing"
}

archivesBaseName = "${rootProject.archives_base_name}-fabric-mc${rootProject.minecraft_version}"

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation("net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}")

    common(project(path: ":common", configuration: "namedElements")) {
        transitive = false
    }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) {
        transitive = false
    }

    modCompileOnly("me.shedaniel:RoughlyEnoughItems-api-fabric:${rootProject.rei_version}")
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${rootProject.rei_version}")
    modCompileOnly("mcp.mobius.waila:wthit-api:fabric-${rootProject.wthit_version}")
    modCompileOnly("curse.maven:jade-324717:${rootProject.jade_fabric_id}")
    modCompileOnly("curse.maven:top-plus-595092:${rootProject.the_one_probe_plus_fabric_id}")

    modRuntimeOnly("me.shedaniel:RoughlyEnoughItems-fabric:${rootProject.rei_version}")
    modRuntimeOnly("mcp.mobius.waila:wthit:fabric-${rootProject.wthit_version}")
    // modRuntimeOnly("curse.maven:jade-324717:${rootProject.jade_fabric_id}")
    // modRuntimeOnly("mcjty.theoneprobe:theoneprobe-fabric:1.18-${rootProject.the_one_probe_fabric_version}")
    // modRuntimeOnly("curse.maven:top-plus-595092:${rootProject.the_one_probe_plus_fabric_id}")

    modRuntimeOnly("com.terraformersmc:modmenu:${rootProject.modmenu_version}")
    modRuntimeOnly("curse.maven:fabric-enchantments-498465:${rootProject.fabric_enchantments_version}")
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

unifiedPublishing {
    project {
        displayName = "WMITAF Fabric $project.version for Minecraft $rootProject.supported_minecraft_versions"
        changelog = "- Added [Jade](https://www.curseforge.com/minecraft/mc-mods/jade \"Jade\") support."
        releaseType = "release"
        gameVersions = ["1.19", "1.19.1", "1.19.2"]
        gameLoaders = ["fabric"]

        mainPublication remapJar

        relations {
            optional {
                curseforge = "roughly-enough-items"
                modrinth = "roughly-enough-items"
            }

            optional {
                curseforge = "wthit"
                modrinth = "wthit"
            }

            optional {
                curseforge = "jade"
            }

            optional {
                curseforge = "top-plus"
            }
        }

        if (project.hasProperty("CURSEFORGE_TOKEN") || System.getenv("CURSEFORGE_TOKEN") != null) {
            curseforge {
                token = project.hasProperty("CURSEFORGE_TOKEN") ? project.property("CURSEFORGE_TOKEN") : System.getenv("CURSEFORGE_TOKEN")
                id = rootProject.curseforge_project_id
                gameVersions.addAll("Java 17")
            }
        }

        if (project.hasProperty("MODRINTH_TOKEN") || System.getenv("MODRINTH_TOKEN") != null) {
            modrinth {
                token = project.hasProperty("MODRINTH_TOKEN") ? project.property("MODRINTH_TOKEN") : System.getenv("MODRINTH_TOKEN")
                id = rootProject.modrinth_project_id
                version = "mc$rootProject.minecraft_version-$project.version-fabric"
            }
        }
    }
}
